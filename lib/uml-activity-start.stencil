(in-package :kaavio-user)

(defparameter *uml-activity-start-radius*     10)
(defparameter *uml-activity-start-fill*   :black)
(defparameter *uml-activity-start-filter*    nil)
(defparameter *uml-activity-start-layer*     nil)

;;------------------------------------------------------------------------------
;;
;; class uml-activity-start
;;
;;------------------------------------------------------------------------------
(defclass uml-activity-start (kaavio:circle) ())


(defmethod initialize-instance :after ((obj uml-activity-start) &rest initargs)
  (declare (ignore initargs))
  ;;(format t "initialize-instance :after called.~%")
  (with-slots (filter layer) obj
    (setf filter  (if (eq filter :none)
                      nil
                      (or filter *uml-activity-start-filter* *default-filter*)))
    (setf layer  (if (eq layer :none)
                     nil
                     (or layer *uml-activity-start-layer* *default-layer*))))
  obj)


;;------------------------------------------------------------------------------------- BEGIN TURNUP
;;#### macro uml-activity-start
;;
;;<!-- stack:push li class='syntax' -->
;;${SYNTAX}
;;
;;* ${{B}{uml-activity-start}} position ${KEY} radius pivot fill link layer filter id
;;
;;<!-- stack:pop li -->
;;
;;${ARGS_AND_VALS}
;;
;;* `position` ---- 描画の基準点を指定します。詳細は「[](#座標と位置)」を参照してください。
;;* `radius` ---- 開始アクションの半径を数値で指定します。省略した場合のデフォルト値は 10 です。
;;* `pivot` ---- `position` で指定した基準点が図形要素のどこにくるように描画するかを指定します。詳細は「[](#座標と位置)」を参照してください。
;;* `fill` ---- 円を塗り潰す色を指定します。省略した場合のデフォルト値は `:black` です。
;;* `link` ---- リンクにする場合、リンク先を指定します。
;;* `layer` ---- レイヤーを指定する場合、その ID をキーワードシンボルで指定します
;;* `filter` ---- フィルタを適用したい場合、その ID をキーワードシンボルで指定します
;;* `id` ---- ID を付与したい場合、その名前をキーワードシンボルで指定します
;;
;;${DESCRIPTION}
;;
;;　UML アクティビティ図における開始アクションを描画します。スタイルを統一したい場合、
;;with-uml-activity-start-options マクロを使うことができます。
;;
;;${SEE_ALSO}
;;
;;* [$$](#アクティビティ図)
;;* [$$](#uml-activity-start)
;;* with-uml-activity-start-options マクロ
;;
;;${NO_NOTES}
;;
;;--------------------------------------------------------------------------------------- END TURNUP
(defmacro uml-activity-start (position &key radius pivot fill link layer filter id)
  `(register-entity (make-instance 'uml-activity-start
                                   :position ,position :pivot ,pivot
                                   :radius (or ,radius *uml-activity-start-radius*)
                                   :fill   (or ,fill   *uml-activity-start-fill* *default-fill*)
                                   :clip-path kaavio:*current-clip-path*
                                   :stroke :none :link ,link :layer ,layer :filter ,filter :id ,id)))


;;------------------------------------------------------------------------------------- BEGIN TURNUP
;;#### macro with-uml-activity-start-options
;;
;;<!-- stack:push li class='syntax' -->
;;${SYNTAX}
;;
;;* ${{B}{with-uml-activity-start-options}} (${KEY} radius fill filter layer) ${BODY} body
;;
;;<!-- stack:pop li -->
;;
;;${DESCRIPTION}
;;
;;　uml-activity-start マクロで描画される図形要素のデフォルトオプションを変更します。
;;キーワードパラメータ群の説明は uml-activity-start マクロを参照してください。
;;
;;${SEE_ALSO}
;;
;;* uml-activity-start マクロ
;;
;;${NO_NOTES}
;;
;;--------------------------------------------------------------------------------------- END TURNUP
(defmacro with-uml-activity-start-options ((&key radius fill filter layer) &rest body)
  (labels ((impl (params acc)
             (if (null params)
                 acc
                 (let ((value  (car  params))
                       (symbol (cadr params)))
                   (impl (cddr params)
                         (if (null value)
                             acc
                             (push (list symbol value) acc)))))))
    (let ((lst (impl (list radius   '*uml-activity-start-radius*
                           fill     '*uml-activity-start-fill*
                           filter   '*uml-activity-start-filter*
                           layer    '*uml-activity-start-layer*) nil)))
      `(let ,lst
         ,@body))))
