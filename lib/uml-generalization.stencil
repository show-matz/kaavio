
(in-package :kaavio-user)

(load-stencil :uml-common-base)
(load-stencil :uml-connection-common)
(load-stencil :uml-stereotype-info)

(defparameter *uml-generalization-stroke*      :black)
(defparameter *uml-generalization-fill*        :white)
(defparameter *uml-generalization-arrow-size* :medium)
(defparameter *uml-generalization-filter*         nil)
(defparameter *uml-generalization-layer*          nil)
(defparameter *uml-generalization-font*           nil)


;;------------------------------------------------------------------------------
;;
;; class uml-generalization
;;
;;------------------------------------------------------------------------------
(defclass uml-generalization (kaavio:connector)
  ((name       :initform nil :initarg :name)           ; (or nil kaavio::label-info)
   (stereotype :initform nil :initarg :stereotype)))   ; (or nil uml-stereotype-info)

(defmethod initialize-instance :after ((gen uml-generalization) &rest initargs)
  (declare (ignore initargs))
  (with-slots (stereotype) gen
    (setf stereotype (and stereotype (make-uml-stereotype stereotype))))
  gen)


(defmethod check ((gen uml-generalization) canvas dict)
  ;; this method must call super class' one.
  (call-next-method)
  (with-slots (name stereotype) gen
    (check-object name       canvas dict :nullable t :class label-info)
    (check-object stereotype canvas dict :nullable t :class uml-stereotype-info))
  nil)

(defmethod entity-composition-p ((gen uml-generalization))
  (with-slots (name stereotype) gen
    (or name
        stereotype
        (call-next-method))))

;;no implementation
;(defmethod post-draw ((gen uml-generalization) writer)
;  (call-next-method))
      


;;------------------------------------------------------------------------------------- BEGIN TURNUP
;;#### macro uml-generalization
;;
;;<!-- stack:push li class='syntax' -->
;;${SYNTAX}
;;
;;* ${{B}{uml-generalization}} from to ${KEY} stereotype keyword name style spacing filter layer id
;;
;;<!-- stack:pop li -->
;;
;;${ARGS_AND_VALS}
;;
;;* `from` ---- 接続元となる要素の ID をキーワードで指定します。point 値も指定できます。
;;* `to` ---- 接続先となる要素の ID をキーワードで指定します。point 値も指定できます。
;;* `stereotype` ---- ステレオタイプを明示する場合はステレオタイプ情報を指定します。詳細は [$@ 節](#uml-stereotype-info) を参照してください。
;;* `keyword` ---- キーワードを明示する場合はステレオタイプ情報を指定します。 `stereotype` が指定されている場合は無視されます。詳細は [$@ 節](#uml-stereotype-info) を参照してください。
;;* `name` ---- 汎化関係に名前をつける場合、ラベル形式で指定します。
;;* `style` ---- 接続線の引き方を指定します。詳細は [$@ 節](#コネクタ)を参照してください。
;;* `spacing` ---- 2 回以上折れ曲がる接続線における「自由な線分」の位置を調整するためのパラメータです。詳細は [$@ 節](#コネクタ)を参照してください。
;;* `filter` ---- フィルタを適用したい場合、その ID をキーワードシンボルで指定します。
;;* `layer` ---- レイヤーを指定する場合、その ID をキーワードシンボルで指定します。
;;* `id` ---- ID を付与したい場合、その名前をキーワードシンボルで指定します。
;;
;;${DESCRIPTION}
;;
;;　クラス図を始めとする各種の UML ダイアグラムにおける汎化関係を描画します。スタイルを統一
;;したい場合、with-uml-generalization-options マクロを使うことができます。
;;
;;${SEE_ALSO}
;;
;;* [$$](#クラス図)
;;* [$$](#uml-generalization)
;;* with-uml-generalization-options マクロ
;;
;;${NO_NOTES}
;;
;;--------------------------------------------------------------------------------------- END TURNUP
(defmacro uml-generalization (from to &key stereotype keyword name style spacing filter layer id)
  (let ((g-stype (gensym "STEREOTYPE"))
        (g-name  (gensym "NAME")))
    `(let ((,g-stype ,(or stereotype keyword))
           (,g-name  ,name))
       (register-entity (make-instance 'uml-generalization
                                       :from ,from :to ,to
                                       :stereotype ,g-stype :style ,style :spacing ,spacing
                                       :name (uml-make-label *uml-generalization-font* ,g-name)
                                       :label (and (or ,g-stype ,g-name)
                                                   #'uml-connection-draw-label)
                                       :stroke (or *uml-generalization-stroke*
                                                   (make-stroke :dasharray nil :base *default-stroke*))
                                       :end1 nil
                                       :end2 (list :type :triangle
                                                   :fill (or *uml-generalization-fill* :white)
                                                   :size *uml-generalization-arrow-size*)
                                       :clip-path *current-clip-path*
                                       :filter (or ,filter *uml-generalization-filter* *default-filter*)
                                       :layer  (or ,layer  *uml-generalization-layer*  *default-layer*)
                                       :id ,id)))))

;;------------------------------------------------------------------------------------- BEGIN TURNUP
;;#### macro with-uml-generalization-options
;;
;;<!-- stack:push li class='syntax' -->
;;${SYNTAX}
;;
;;* ${{B}{with-uml-generalization-options}} (${KEY} stroke fill arrow-size font filter layer) ${BODY} body
;;
;;<!-- stack:pop li -->
;;
;;${DESCRIPTION}
;;
;;　uml-generalization マクロで描画される図形要素のデフォルトオプションを変更します。
;;キーワードパラメータ群の説明は uml-generalization マクロを参照してください。
;;
;;${SEE_ALSO}
;;
;;* uml-generalization マクロ
;;
;;${NO_NOTES}
;;
;;--------------------------------------------------------------------------------------- END TURNUP
(defmacro with-uml-generalization-options ((&key (stroke     nil stroke-p)
                                                 (fill       nil fill-p)
                                                 (arrow-size nil arrow-size-p)
                                                 (font       nil font-p)
                                                 (filter     nil filter-p)
                                                 (layer      nil layer-p)) &rest body)
  (let ((bindings nil))
    (labels ((impl (arg-p binding)
               (when arg-p (push binding bindings))))
      (impl arrow-size-p `(*uml-generalization-arrow-size* ,arrow-size))
      (impl stroke-p `(*uml-generalization-stroke* (make-stroke2 *uml-generalization-stroke* ,stroke)))
      (impl fill-p   `(*uml-generalization-fill*   (make-fill2   *uml-generalization-fill*   ,fill)))
      (impl font-p   `(*uml-generalization-font*   (make-font2   *uml-generalization-font*   ,font)))
      (impl filter-p `(*uml-generalization-filter* ,filter))
      (impl layer-p  `(*uml-generalization-layer*  ,layer)))
    `(let ,(nreverse bindings)
       ,@body)))

