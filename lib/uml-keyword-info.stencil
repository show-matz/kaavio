
(in-package :kaavio-user)

(defparameter *uml-keyword-font*  nil)

;;------------------------------------------------------------------------------
;;
;; class uml-keyword-info
;;
;;------------------------------------------------------------------------------
(defclass uml-keyword-info ()
  ((name	:initform nil :initarg :name)	; (or keyword string)
   (font	:initform nil :initarg :font)))	; (or nil font-info)


(defmethod initialize-instance :after ((kwd uml-keyword-info) &rest initargs)
  (declare (ignore initargs))
  (with-slots (font) kwd
	(when font
	  (setf font (make-font font)))))

(defmethod check ((kwd uml-keyword-info) canvas dict)
  (with-slots (name font) kwd
	(check-member name  :nullable nil :types (or keyword string))
	(check-object font  canvas dict :nullable t :class font-info))
  nil)

(defun uml-keyword-to-string (kwd)
  (format-string "<<" (slot-value kwd 'name) ">>"))

(defun uml-keyword-draw (kwd x y align writer)
  (let ((txt (uml-keyword-to-string kwd))
		(txt-anchor (ecase align
							((:left)   "start")
							((:center) "middle")
							((:right)  "end"))))
	(with-slots (font) kwd
	  (write-text-tag x y txt-anchor txt writer :font font))))


(defun uml-keyword-draw-beside-line (kwd line writer)
  (multiple-value-bind (x y sin cos) (kaavio::line-get-center line)	;;ToDo : export!?
	(declare (ignorable x y sin cos))
	(let* ((f-size (slot-value (slot-value kwd 'font) 'kaavio::size))
		   (offset f-size)
		   (align (cond ((zerop sin) :center)
						((<  0  sin) :left)
						(t           :right))))
	  (incf y (/ f-size 2))
	  (uml-keyword-draw kwd (+ x (* sin offset))
							(- y (* cos offset))  align writer))))


(defun make-uml-keyword (&rest params)
  (if (= 1 (length params))
	  (let ((param (car params)))
		(cond
		  ((typep param 'uml-keyword-info) param)
		  ((listp param) (apply #'make-uml-keyword param))
		  (t             (make-uml-keyword :name param))))
	  (destructuring-bind (&key (name  nil  name-p)
								(font  nil  font-p)) params
		(make-instance 'uml-keyword-info
					   :name  (if name-p  name  nil)
					   :font  (if font-p  font  (or *uml-keyword-font*
													kaavio:*default-font*))))))

